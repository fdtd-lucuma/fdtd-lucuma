# Maintainer: Otreblan <otreblain@gmail.com>

# shellcheck shell=bash disable=SC2034,SC2154,SC2164

pkgname=fdtd-lucuma-git
pkgver=0.1.1.r168.ga853720
pkgrel=1
epoch=
pkgdesc="Una GUI para fdtd"
arch=('x86_64')
url="https://github.com/fdtd-lucuma/fdtd-lucuma"
license=('GPL-3.0-or-later')
groups=()
depends=(
	'fmt'
	'gcc-libs'
	'glfw'
	'libxrandr'
	'vulkan-icd-loader'
	'yaml-cpp'
)
makedepends=(
	'bash-completion'
	'clang'
	'cmake'
	'cpp-taskflow'
	'entt'
	'git'
	'glm'
	'llvm'
	'ninja'
	'shader-slang'
	'vulkan-headers'
	'vulkan-memory-allocator'
	'vulkan-validation-layers'
	'xdg-utils-cxx'
)
checkdepends=()
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_gitrepo="$(git rev-parse --show-toplevel)"
_headcommit="$(git rev-parse HEAD)"

#"vma-hpp-git::git+https://github.com/YaaZ/VulkanMemoryAllocator-Hpp#tag=v3.2.1"
source=(
	"$pkgname::git+file://$_gitrepo#commit=$_headcommit"
	"imgui-git::git+https://github.com/ocornut/imgui#branch=docking"
	"imgui-module-git::git+https://github.com/stripe2933/imgui-module#branch=docking"
	"vma-hpp-git::git+https://github.com/otreblan/VulkanMemoryAllocator-Hpp#branch=master"
	"vkfw-git::git+https://github.com/Cvelth/vkfw#branch=main"
	"magic-enum-git::git+https://github.com/Neargye/magic_enum#tag=v0.9.7"
	"mdspan-git::git+https://github.com/kokkos/mdspan#branch=stable"
)
sha256sums=(
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
)

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cmake -G Ninja -B build -S "$pkgname" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_UNITY_BUILD=ON \
		-DUSE_SYSTEM_LIBS=ON \
		-DFETCHCONTENT_FULLY_DISCONNECTED=ON \
		-DFETCHCONTENT_SOURCE_DIR_IMGUI="$srcdir/imgui-git" \
		-DFETCHCONTENT_SOURCE_DIR_IMGUI-MODULE="$srcdir/imgui-module-git" \
		-DFETCHCONTENT_SOURCE_DIR_VULKANMEMORYALLOCATOR-HPP="$srcdir/vma-hpp-git" \
		-DFETCHCONTENT_SOURCE_DIR_VKFW="$srcdir/vkfw-git" \
		-DFETCHCONTENT_SOURCE_DIR_MAGIC_ENUM="$srcdir/magic-enum-git" \
		-DFETCHCONTENT_SOURCE_DIR_MDSPAN="$srcdir/mdspan-git" \
		-DCMAKE_CXX_VISIBILITY_PRESET=hidden \
		-DCMAKE_C_VISIBILITY_PRESET=hidden \

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
