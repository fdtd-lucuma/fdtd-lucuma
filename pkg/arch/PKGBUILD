# Maintainer: Otreblan <otreblain@gmail.com>

# shellcheck shell=bash disable=SC2034,SC2154,SC2164

pkgname=fdtd-vulkan-git
pkgver=r31.3e8a8bd
pkgrel=1
epoch=
pkgdesc="Una GUI para fdtd"
arch=('x86_64')
url="https://github.com/fdtd-lucuma/fdtd-vulkan"
license=('GPL3')
groups=()
depends=(
	'gcc-libs'
	'glfw'
	'vulkan-icd-loader'
)
makedepends=(
	'bash-completion'
	'cmake'
	'entt'
	'git'
	'glm'
	'vulkan-headers'
	'vulkan-memory-allocator'
)
checkdepends=()
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_gitrepo="$(git rev-parse --show-toplevel)"
_headcommit="$(git rev-parse HEAD)"
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cmake -B build -S "$pkgname" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_UNITY_BUILD=ON \
		-DUSE_SYSTEM_LIBS=ON \

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
